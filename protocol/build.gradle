buildscript {
    ext {
        protobufPluginVersion = "0.8.3"
        grpcVersion           = "1.5.0"
        protocVersion         = "3.3.0"
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:$protobufPluginVersion"
    }
}

apply plugin: 'java'
apply plugin: 'com.google.protobuf'

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    jcenter()
}

dependencies {
    compile "io.grpc:grpc-netty:$grpcVersion"
    compile "io.grpc:grpc-protobuf:$grpcVersion"
    compile "io.grpc:grpc-stub:$grpcVersion"
}

protobuf {
    generatedFilesBaseDir = "$projectDir/src"

    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }

    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = 'java'
            }
        }
    }
}

clean {
    delete "${protobuf.generatedFilesBaseDir}/main/java"
}
